;================================== 
; Script Name: Shuriken & Fukiya Dart Restacker & Poisoner
; Author: snicker7 
; Version: 2.0 
; Client Tested with: 4.0.5b 
; EUO version tested with: 1.42.009B 
; Shard OSI / FS:  OSI 
; Revision Date: 11/10/04 
; Public Release: 11/7/05 
; Global Variables Used: N/A 
; Purpose: Makes fukiya darts and shurikens with 10 uses and poisons them.
;==================================
; INSTRUCTIONS:
; =================================
; Get a leather ninja belt or a fukiya. Make a bunch of shurikens 
; or fukiya darts. Press play! Make sure the ninja belt or fukiya
; is in your backpack, not on your paperdoll. The only way this
; will work correctly is with don't move mouse cursor turned off. 
; I repeat:
; ==================================
; THE ONLY WAY THIS SCRIPT WILL WORK 
; IS WITH DON'T MOVE CURSOR OFF.
; (Go to Tools, Options, Uncheck "Don't Move Cursor".)
; ==================================
; For Poisoning:
; Get lots of whatever type of poison potion you 
; want to use in your pack. Support for kegs
; in a later version. If you are going to cure with
; bandages, make sure you have some. Same for
; magery, make sure you have the LRC or the regs.

; START OF SCRIPT

initevents

set %fukiyatype MDP
set %darttype SGP
set %belttype MCP
set %shurikentype GDP
set %poisons AVF

gosub showMenu
checkmenu:
menu get chkPoisoning
if #menures = -1
{
     if %listshown <> 1
          {
                menu Font Color Maroon
	menu Text psntypelabel 152 40 Poison to use:
	menu Font Color WindowText
	menu Font BGColor Window
	menu List Create psntype 152 56 77 45
	menu List Add psntype Lesser
	menu List Add psntype Regular
	menu List Add psntype Greater
	menu List Add psntype Deadly
                set %listshown 1
          }
}
if #menures = 0
{
  set %listshown 0
  menu delete psntype
  menu delete psntypelabel
}
if #menubutton = btnStart
  goto start
wait 1
goto checkmenu

start:
set #menubutton N/A
menu delete status
menu Font Color Silver
menu Text status 8 20 Setting variables.
set %dartsloaded N/A
set %shurikenloaded N/A
menu delete btnStart
menu get chkShurikens
set %doShurikens #menures
menu get chkFukiya
set %doFukiya #menures
menu get chkPoisoning
set %doPoison #menures
if %doPoison = -1
  {
    menu get psntype
    if #menures = 1
      set %psntype lesser
    if #menures = 2
      set %psntype regular
    if #menures = 3
      set %psntype greater
    if #menures = 4
      set %psntype deadly
    menu delete psntype
    menu delete psntypelabel
  }
menu delete chkFukiya
menu delete chkShurikens
menu delete chkPoisoning
menu delete EUOLabel2
menu Font Color Green
if %doPoison = -1
{
                menu Font Color Maroon
	menu Text healinglabel 152 40 Choose Healing Method:
	menu Font Color WindowText
	menu Font BGColor Window
	menu List Create healing 152 56 77 45
	menu List Add healing Bandages
	menu List Add healing Magery
	menu List Add healing Chivalry
                menu Button btnOkay 180 112 55 21 Okay
                waitforokay:
                if #menubutton = btnOkay
                  goto sethealing
                wait 1
                goto waitforokay
                sethealing:
	set #menubutton N/A
                menu get healing
                set %curemethod #menures
                menu delete healinglabel
	menu delete healing
	menu delete btnokay
}
menu Button btnPause 180 112 55 21 Pause

if %doShurikens = -1
  gosub doShurikens
if %doFukiya = -1
  gosub doFukiyas
if %doPoison = -1
  gosub doPoison

  menu delete status
  menu Font Color Silver
  menu Text status 8 20 All done.
halt

sub doFukiyas
loadfukiya:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Fukiya: Loading
  finditem %darttype C_ , #backpackid
  if #findkind = -1
    {
        menu delete status
        menu Font Color Silver
        menu Text status 8 20 Fukiya: Out of darts, moving on...
        return
    }
  event property #findid
  str pos #property :
  str del #property 1 #strRes
  str left #strRes 3
  if 10 in #strRes
  {
    ignoreitem #findid
    goto loadfukiya
  }  
  finditem %fukiyatype C_ , #backpackid
  if #findkind = -1
    {
      display ok You have no fukiya!$ Press ok to move on....
      return
    }
  wait 15
  event property #findid
  str pos #property :
  str del #property 1 #strRes
  str left #strRes 3
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Fukiya: Loading ( #strRes uses on Fukiya)
  if 10 in #strRes
    goto unloadfukiya
  set #findmod 79_3
  click #findx #findy
  wait 20
  set %clickx #findx + 60
  set %clicky #findy + 20
  click %clickx %clicky
  target
  finditem %darttype C_ , #backpackid
  set #ltargetid #findid
  set #ltargetkind 1
  event macro 22 0
goto loadfukiya

unloadfukiya:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Fukiya: Unloading
  finditem %fukiyatype C_ , #backpackid
  if #findkind = -1
    {
      display ok Where did your fukiya go? You just had one!$ Press ok to move on...
      return
    }
  set #findmod 79_3
  click #findx #findy
  wait 20
  set %clickx #findx + 60
  set %clicky #findy + 40
  click %clickx %clicky
goto loadfukiya
return

sub doShurikens
loadbelt:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Shuriken: Loading
  finditem %shurikentype C_ , #backpackid
  if #findkind = -1
    {
      menu delete status
      menu Font Color Silver
      menu Text status 8 20 Shuriken: Out of Shuriken, moving on...
      return
    }
  event property #findid
  str pos #property :
  str del #property 1 #strRes
  str left #strRes 3
  if 10 in #strRes
  {
    ignoreitem #findid
    goto loadbelt
  }  
  finditem %belttype C_ , #backpackid
  if #findkind = -1
    {
      display ok You have no ninja belt!$ Press ok to move on...
      return
    }
  wait 15
  event property #findid
  str pos #property :
  str del #property 1 #strRes
  str left #strRes 3
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Shuriken: Loading ( #strRes uses on belt)
  if 10 in #strRes
    goto unloadbelt
  set #findmod 15_10
  click #findx #findy
  wait 20
  set %clickx #findx + 60
  set %clicky #findy + 20
  click %clickx %clicky
  target
  finditem %shurikentype C_ , #backpackid
  set #ltargetid #findid
  set #ltargetkind 1
  event macro 22 0
goto loadbelt

unloadbelt:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Shuriken: Unloading
  finditem %belttype C_ , #backpackid
  if #findkind = -1
    {
      display ok Where did your ninja belt go? You just had one!$ Press ok to move on...
      return
    }
  set #findmod 15_10
  click #findx #findy
  wait 20
  set %clickx #findx + 60
  set %clicky #findy + 40
  click %clickx %clicky
goto loadbelt
return

sub doPoison
  ignoreitem reset
  psnfukiya:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Poison: Applying to Fukiya Darts.
  finditem %darttype C_ , #backpackid
  if #findkind = -1
    goto psnshuriken
  set %psndartid #findid
  gosub findpsn
  if #result = notfound
    {
      display ok You do not have any %psntype poison potions in your backpack!!!
      return
    }
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Poison: Applying to Fukiya Darts.
  event macro 13 30
  target
  set #ltargetid #result
  set #ltargettype 1
  event macro 22 0
  target
  set #ltargetid %psndartid
  set #ltargettype 1
  event macro 22 0
  set %_jindex #jindex
  scanjournal %_jindex
  if YOU_APPLY_THE_POISON. in #journal
     ignoreitem %psndartid 
  gosub checkforpoison
  wait 10s
  goto psnfukiya
  psnshuriken:
gosub checkforpause
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Poison: Applying to Shurikens.
    finditem %shurikentype C_ , #backpackid
  if #findkind = -1
    return
  set %psnshurikenid #findid
  gosub findpsn
  if #result = notfound
    {
      display ok You do not have any %psntype poison potions in your backpack!!!
      return
    }
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Poison: Applying to Shurikens.
  event macro 13 30
  target
  set #ltargetid #result
  set #ltargettype 1
  event macro 22 0
  target
  set #ltargetid %psnshurikenid
  set #ltargettype 1
  event macro 22 0
  set %_jindex #jindex
  scanjournal %_jindex
  if YOU_APPLY_THE_POISON. in #journal
     ignoreitem %psnshurikenid
  gosub checkforpoison
  wait 10s
  goto psnshuriken
return

sub findpsn
  findpsn:
  menu delete status
  menu Font Color Silver
  menu Text status 8 20 Poison: Searching for %psntype poison potion.
  finditem %poisons C_ , #backpackid
  if #findkind = -1
     return notfound
  event property #findid
  if %psntype = lesser
    {
      if LESSER notin #property
        {
          ignoreitem #findid
          goto findpsn
        }
      return #findid
    }
  if %psntype = regular
    {
      if A_POISON_POTION notin #property
        {
          ignoreitem #findid
          goto findpsn
        }
      return #findid
    }
  if %psntype = greater
    {
      if greater notin #property
        {
          ignoreitem #findid
          goto findpsn
        }
      return #findid
    }
  if %psntype = deadly
    {
      if deadly notin #property
        {
          ignoreitem #findid
          goto findpsn
        }
      return #findid
    }
return notfound

sub checkforpoison
  checkforpsn:
  if C in #charstatus
    {
      if %curemethod = 1
        {
          finditem ZLF C_ , #backpackid
          if #findkind <> 1
          {
             display ok NO BANDAGES?!?!?!
             return
          }          
          set #lobjectid #findid
          event macro 17 0
          target
          event macro 23 0
          wait 4s
          goto checkforpsn
        }
       if %curemethod = 2
        {
          event macro 15 10
          target 3s
          event macro 23 0
          wait 10
          goto checkforpsn
         }
        if %curemethod = 3
         {
           event macro 15 201
           target 3s
           event macro 23 0
           wait 10
           goto checkforpsn
          }
        }
return

sub checkforpause
if #menubutton = btnPause
{
set #menubutton N/A
menu delete btnPause
menu Button btnResume 180 112 55 21 Resume
waitforresume:
if #menubutton = btnResume
  goto resumenow
wait 1
goto waitforresume
resumenow:
menu delete btnResume
menu Button btnPause 180 112 55 21 Pause
}
return

;--------- EasyUO Menu Designer Code Begin ---------
sub showMenu
	menu Clear
	menu Window Title Snicker's Shuriken & Dart Restacker Poisoner
	menu Window Color Black
	menu Window Size 238 135
	menu Font Transparent #true
	menu Font Align Right
	menu Font Name MS Sans Serif
	menu Font Size 8
	menu Font Style 
	menu Font Color Maroon
	menu Font Transparent #false
	menu Font Align Left
	menu Font BGColor Black
	menu Text EUOLabel1 4 4 Status:
	menu Font Color Silver
	menu Text status 8 20 Waiting for input.
	menu Font Color Maroon
	menu Text EUOLabel2 4 40 What to do:
	menu Font Color Silver
	menu Check chkShurikens 8 56 97 17 #false Shurikens
	menu Check chkFukiya 8 72 97 17 #false Fukiya
	menu Check chkPoisoning 8 88 97 17 #false Poisoning
	menu Font Color Green
	menu Button btnStart 180 112 55 21 Start
	menu Show
return
;--------- EasyUO Menu Designer Code End ---------
